// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).


// The ExtendAlu chip is an extension of the standard ALU which also supports
// shift operations.
// The inputs of the extended ALU are instruction[9], x[16], y[16].
// The "ng" and "zr" output pins behave the same as in the regular ALU.
// The "out" output is defined as follows:
// If instruction[8]=1 and instruction[7]=1 the output is identical to the 
// regular ALU, where:
// instruction[5]=zx, instruction[4]=nx, ..., instruction[0]=no
// Else, if instruction[8]=0 and instruction[7]=1, the output is a shift:
// - If instruction[4] == 0, the input "y" will be shifted, otherwise "x".
// - If instruction[5] == 0, the shift will be a right-shift, otherwise left.
// - All other inputs are undefined.

CHIP ExtendAlu {
     IN x[16], y[16], instruction[9];
     OUT out[16], zr, ng;
     
     PARTS:
     ALU(x=x, y=y, zx=instruction[5], nx=instruction[4], zy=instruction[3], ny=instruction[2], f=instruction[1], no=instruction[0], out=regout, zr=regzr, ng=regng);
     Nand(a=instruction[8], b=instruction[7], out=newalu);

     Mux16(a=y, b=x, sel=instruction[4], out=toshift);
     ShiftRight(in=toshift, out=rightshift);
     ShiftLeft(in=toshift, out=leftshift);
     Mux16(a=rightshift, b=leftshift, sel=instruction[5], out[0..7]=newout1, out[8..15]=newout2, out[15]=isneg);
     Or8Way(in=newout1, out=newzr1);
     Or8Way(in=newout2, out=newzr2);
     Or(a=newzr1, b=newzr2, out=checknewzr);
     Not(in=checknewzr, out=newzr);

     Mux(a=regzr, b=newzr, sel=newalu, out=zr);
     Mux(a=regng, b=isneg, sel=newalu, out=ng);
     Mux16(a=regout, b[0..7]=newout1, b[8..15]=newout2, sel=newalu, out=out);
}
