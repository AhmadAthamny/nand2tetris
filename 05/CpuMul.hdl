// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

// This chip is an extension of the regular CPU that uses the extended ALU.
// If instruction[15]==0 or (instruction[14]==1 and instruction[13]==1),
// then CpuMul behaves exactly the same as the regular CPU.
// If instruction[15]==1 and instruction[14]==0 the chip will behave as follows:
// | Instruction           | 15 | 14 | 13 | a | c1 | c2 | c3 | c4 | c5 | c6 |
// |-----------------------|:--:|:--:|:--:|:-:|:--:|:--:|:--:|:--:|:--:|:--:|
// | Regular a-instruction |  0 |  * |  * | * |  * |  * |  * |  * |  * |  * |
// | Regular c-instruction |  1 |  1 |  1 | * |  * |  * |  * |  * |  * |  * |
// | dest=A<<;jump         |  1 |  0 |  1 | 0 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=D<<;jump         |  1 |  0 |  1 | 0 |  1 |  1 |  0 |  0 |  0 |  0 |
// | dest=M<<;jump         |  1 |  0 |  1 | 1 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=A>>;jump         |  1 |  0 |  1 | 0 |  0 |  0 |  0 |  0 |  0 |  0 |
// | dest=D>>;jump         |  1 |  0 |  1 | 0 |  0 |  1 |  0 |  0 |  0 |  0 |
// | dest=M>>;jump         |  1 |  0 |  1 | 1 |  0 |  0 |  0 |  0 |  0 |  0 |
// Where:
// - "<<" is a left shift, and ">>" is a right shift, as defined in project 2.
//   These notations were chosen because they are used in real programming
//   languages.
// - dest and jump can take the same values as in the regular CPU.

CHIP CpuMul {
    IN  
        inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).
    OUT 
        outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction
    
    PARTS:
    // Sel and load pins.
    And(a=instruction[15], b=instruction[5], out=cinstrload);
    Not(in=instruction[15], out=ainstruction);
    Or(a=ainstruction, b=cinstrload, out=loada);
    And(a=instruction[15], b=instruction[4], out=loadd);
    And(a=instruction[15], b=instruction[3], out=writeM);

    // Getting the correct output from the ARegister
    Mux16(a=aluout, b=instruction, sel=ainstruction, out=ainput);
    ARegister(in=ainput, load=loada, out=aoutput, out[0..14]=addressM);

    // Passing pins to the ExtendAlu chip.
    Mux16(a=aoutput, b=inM, sel=instruction[12], out=ypin);
    DRegister(in=aluout, load=loadd, out=doutput);
    ExtendAlu(x=doutput, y=ypin, instruction=instruction[6..14], out=outM, out=aluout, zr=zrout, ng=ngout);

    // Calculating the new PC value (reset/load/inc..)
    And(a=zrout, b=instruction[1], out=jmpzero);
    And(a=ngout, b=instruction[2], out=jmpneg);
    Or(a=zrout, b=ngout, out=zeroneg);
    Not(in=zeroneg, out=positive);
    And(a=positive, b=instruction[0], out=jmppos);
    Or(a=jmpzero, b=jmpneg, out=jmpzeroneg);
    Or(a=jmpzeroneg, b=jmppos, out=jumpcond);
    And(a=jumpcond, b=instruction[15], out=dojump);

    PC(in=aoutput, load=dojump, inc=true, reset=reset, out[0..14]=pc);
}
